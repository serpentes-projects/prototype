plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "5.3.0"
    id 'idea'
}

group 'io.serpentes'
version '0.0.1-PROTOTYPE'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    //TODO: Check public api-usage.
    implementation group: 'jakarta.enterprise', name: 'jakarta.enterprise.cdi-api', version: '3.0.0'
    implementation group: 'org.jboss.weld.se', name: 'weld-se-core', version: '4.0.1.Final'
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.18'

    api project(':modules:api')
    api project(':modules:definition:sources:file-system:file-system-definition-source-api')
    //TODO: Get rid of this dependency.
    implementation project(':modules:definition:trees:type-tree')

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.6.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

testClasses {
    doLast {
        // FIXME: For some reason this is necessary to ensures some of the integration-tests succeed.
        copy {
            from('src/main/resources') { include 'META-INF/beans.xml' }
            into "$buildDir/classes/java/main/"
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        fileSystemDefinitionSourceImplementation(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}